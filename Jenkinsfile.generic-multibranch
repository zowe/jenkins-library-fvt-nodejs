/*
 * This program and the accompanying materials are made available under the terms of the
 * Eclipse Public License v2.0 which accompanies this distribution, and is available at
 * https://www.eclipse.org/legal/epl-v20.html
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Copyright Contributors to the Zowe Project.
 */

/**
 * These 2 build parameters are only required for running integration test
 */
def opts = []
// define custom build parameters
def customParameters = []
customParameters.push(booleanParam(
  name: 'FETCH_PARAMETER_ONLY',
  description: 'By default, the pipeline will exit just for fetching parameters.',
  defaultValue: true
))
customParameters.push(string(
  name: 'LIBRARY_BRANCH',
  description: 'Jenkins library branch to test',
  defaultValue: '',
  trim: true
))
opts.push(parameters(customParameters))

// set build properties
properties(opts)

/**
 * This check is only required for running integration test
 */
if (params.FETCH_PARAMETER_ONLY) {
    currentBuild.result = 'NOT_BUILT'
    error "Prematurely exit after fetching parameters."
}

node('ibm-jenkins-slave-nvm') {
    /**
     * This section is only required for running integration test.
     *
     * In real consumption of library, we should use default library branch. For
     * example:
     *
     * def lib = library("jenkins-library").org.zowe.jenkins_shared_library
     */
    def branch = 'master'

    if (params.LIBRARY_BRANCH) {
        branch = params.LIBRARY_BRANCH
    } else if (env.CHANGE_BRANCH) {
        branch = env.CHANGE_BRANCH
    } else if (env.BRANCH_NAME) {
        branch = env.BRANCH_NAME
    }

    echo "Jenkins library branch $branch will be used to build."
    def lib = library("jenkins-library@$branch").org.zowe.jenkins_shared_library

    def pipeline = lib.pipelines.generic.GenericPipeline.new(this)

    /**
     * These 2 build parameters are only required for running integration test
     */
    pipeline.addBuildParameters(customParameters)

    pipeline.admins.add("jackjia")

    pipeline.setup(
        packageName: 'org.zowe.jenkins-library-test.nodejs',
        github: [
            email                      : lib.Constants.DEFAULT_GITHUB_ROBOT_EMAIL,
            usernamePasswordCredential : lib.Constants.DEFAULT_GITHUB_ROBOT_CREDENTIAL,
        ],
        artifactory: [
            url                        : lib.Constants.DEFAULT_ARTIFACTORY_URL,
            usernamePasswordCredential : lib.Constants.DEFAULT_ARTIFACTORY_ROBOT_CREDENTIAL,
        ],
        pax: [
            sshHost                    : lib.Constants.DEFAULT_PAX_PACKAGING_SSH_HOST,
            sshPort                    : lib.Constants.DEFAULT_PAX_PACKAGING_SSH_PORT,
            sshCredential              : lib.Constants.DEFAULT_PAX_PACKAGING_SSH_CREDENTIAL,
            remoteWorkspace            : lib.Constants.DEFAULT_PAX_PACKAGING_REMOTE_WORKSPACE,
        ],
        extraInit: {
            // get current version, which is required by uploading artifacts
            def npm = lib.npm.Registry.new(this)
            def packageInfo = npm.getPackageInfo()
            pipeline.setVersion(packageInfo['version'])
        }
    )

    pipeline.build(
        timeout       : [time: 5, unit: 'MINUTES'],
        operation     : {
            sh "npm install && npm run build"
        }
    )

    pipeline.createStage(
        name          : "CustomStage",
        isSkippable   : true,
        stage         : {
            echo "This is a custom stage, skippable"
        },
        timeout: [time: 1, unit: 'MINUTES']
    )

    def UNIT_TEST_ROOT = "__tests__/__results__"

    pipeline.test(
        name          : "Unit",
        operation     : {
            sh "npm run test:unit"
        },
        junit         : "${UNIT_TEST_ROOT}/unit/junit.xml",
        cobertura     : [
            // do not mark as UNSTABLE if not pass the requirement
            autoUpdateStability       : false,
            fileCoverageTargets       : '0, 0, 0',
            classCoverageTargets      : '0, 0, 0',
            methodCoverageTargets     : '0, 0, 0',
            lineCoverageTargets       : '0, 0, 0',
            conditionalCoverageTargets: '0, 0, 0',
            coberturaReportFile       : "${UNIT_TEST_ROOT}/unit/coverage/cobertura-coverage.xml"
        ],
        htmlReports   : [
            [dir: "${UNIT_TEST_ROOT}/jest-stare", files: "index.html", name: "Report: Jest Stare"],
            [dir: "${UNIT_TEST_ROOT}", files: "results.html", name: "Report: Unit Test"],
            [dir: "${UNIT_TEST_ROOT}/unit/coverage/lcov-report", files: "index.html", name: "Report: Code Coverage"],
        ],
    )

    // default packaging operation
    pipeline.packaging(name: 'jenkins-library-fvt-nodejs')

    pipeline.publish(
        allowPublishPreReleaseFromFormalReleaseBranch: true,
        artifacts: [
            '.pax/jenkins-library-fvt-nodejs.pax'
        ]
    )

    pipeline.createStage(
        name          : "Verify Artifact Uploaded",
        isSkippable   : true,
        stage         : {
            def artifactory = pipeline.getArtifactory()
            def artifact = artifactory.getArtifact(
                // this should be a snapshot publish because we are not do a release
                pattern        : "${pipeline.isPerformingRelease() ? artifactory.REPOSITORY_RELEASE : artifactory.REPOSITORY_SNAPSHOT}/**/jenkins-library-fvt-nodejs-*",
                'build-name'   : env.JOB_NAME,
                'build-number' : env.BUILD_NUMBER,
            )
            echo "Successfully found artifact uploaded: ${artifact}"
        },
        timeout: [time: 5, unit: 'MINUTES']
    )

    // define we need release stage
    pipeline.release(
        bumpVersion: {
            echo 'Customized bump version started ...'
            // bump patch level
            sh "git reset --hard && git clean -f && npm version patch"
            // amend last commit with sign-off
            def commitMessage = sh(script: "git log -1 --pretty=%s", returnStdout: true).trim()
            sh "git reset HEAD~1 && git add . && git commit -s -m \"${commitMessage}\""
            // push to remote
            sh "git push origin HEAD:${env.BRANCH_NAME}"
        }
    )

    pipeline.end()
}
